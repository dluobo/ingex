AC_INIT(src/ingexgui.cpp)
AM_INIT_AUTOMAKE(ingexgui, 0.1)

AC_PROG_CXX
AC_PROG_INSTALL
AC_LIBTOOL_DLOPEN
AC_PROG_LIBTOOL

CPPFLAGS="$CPPFLAGS -Wall -g -fexceptions"
CXXFLAGS="$CXXFLAGS -Wall -g -fexceptions"

WXCONFIG=wx-config
AC_ARG_WITH(wx-config,
[[  --with-wx-config=FILE     Use the given path to wx-config when determining
                            wxWidgets configuration; defaults to "wx-config"]],
[
    if test "$withval" != "yes" -a "$withval" != ""; then
        WXCONFIG=$withval
    fi
])

wxversion=0

AC_DEFUN([WXTEST],
[
	AC_REQUIRE([AC_PROG_AWK])
	AC_MSG_CHECKING([wxWidgets version])
	if wxversion=`$WXCONFIG --version`; then
		AC_MSG_RESULT([$wxversion])
	else
		AC_MSG_RESULT([not found])
		AC_MSG_ERROR([wxWidgets is required. Try --with-wx-config.])
	fi])

# Call WXTEST func
WXTEST

# Verify minimus requires
vers=`echo $wxversion | $AWK 'BEGIN { FS = "."; } { printf "% d", ($1 * 1000 + $2) * 1000 + $3;}'`
if test -n "$vers" && test "$vers" -ge 2003003; then
	WX_CPPFLAGS="`$WXCONFIG --cppflags`"
	WX_CXXFLAGS="`$WXCONFIG --cxxflags | sed -e 's/-fno-exceptions//'`"
	WX_LIBS="`$WXCONFIG --libs`"
else
	AC_MSG_ERROR([wxWidgets 2.3.3 or newer is required])
fi

# WORKSPACE is set to the ingex top-level directory
WORKSPACE=`pwd | xargs dirname | xargs dirname | xargs dirname`

AC_DEFUN([PLAYERTEST],
[
	PLAYERLIB="libIngexPlayer.a"
	AC_MSG_CHECKING([for $PLAYERLIB])
	if test -f "$WORKSPACE/player/IngexPlayer/$PLAYERLIB"; then
		AC_MSG_RESULT([yes])
		AC_MSG_CHECKING([player for DVS card support])
		if test `readelf -s $WORKSPACE/player/IngexPlayer/$PLAYERLIB | grep -c dvs_register_listener` -gt 0; then
			AC_MSG_RESULT([yes])
			DVS_CPPFLAGS="-DHAVE_DVS"
			AC_MSG_CHECKING([for DVS SDK])
			DVS_PATH=`for f in /usr/local /usr; do test -e $f/include/dvs_fifo.h && echo $f && break; done`
			if test -n "$DVS_PATH"; then
				AC_MSG_RESULT([yes])
				DVS_LDFLAGS="-L$DVS_PATH/lib -L$DVS_PATH/lib64 -ldvsoem"
			else 
				if test -n "$DVSSDK"; then
					# DVS SDK version 3		
					AC_MSG_RESULT([yes])
					DVS_LDFLAGS="-L$DVSSDK/linux-x86/lib -L$DVSSDK/linux-x86_64/lib -ldvsoem"
				else
					if test -n "$DVS_SDK_INSTALL"; then
						# DVS SDK version 2		
						AC_MSG_RESULT([yes])
						DVS_LDFLAGS="-L$DVS_SDK_INSTALL/linux/lib -ldvsoem"
					else
						AC_MSG_RESULT([no])
						AC_MSG_ERROR([Cannot find DVS SDK: set the DVSSDK or DVS_SDK_INSTALL environment variable to the location of your installed DVS SDK version 3 or version 2 respectively])
					fi
				fi
			fi
		else
			AC_MSG_RESULT([no])
		fi
	else
		AC_MSG_RESULT([no])
		AC_MSG_ERROR([You need to build the Ingex Player first.])
	fi
])

#Call PLAYERTEST func
PLAYERTEST

AC_DEFUN([ACETAOTEST],
[
	AC_MSG_CHECKING([for ACE_ROOT and TAO_ROOT variables])
	if test -n "$ACE_ROOT" && test -n "$TAO_ROOT"; then
		AC_MSG_RESULT([yes])
	else
		AC_MSG_RESULT([no])
		AC_MSG_WARN([You need to define ACE_ROOT and TAO_ROOT environment variables before you build the project.])
	fi
	IDLLOC="$WORKSPACE/studio/ace-tao/IDL"
	IDLLIB="libidl.a"
	AC_MSG_CHECKING([for $IDLLIB])
	if test -f "$IDLLOC/$IDLLIB"; then
		AC_MSG_RESULT([yes])
	else
		AC_MSG_RESULT([no])
		AC_MSG_ERROR([You need to build the IDL library first.])
	fi
])

#call ACETAOTEST func
ACETAOTEST

CPPFLAGS="$CPPFLAGS $WX_CPPFLAGS -I$ACE_ROOT -I$TAO_ROOT -I$TAO_ROOT/orbsvcs -I$IDLLOC/Generated -I$WORKSPACE/player/ingex_player -I$WORKSPACE/player/IngexPlayer -I$WORKSPACE/libMXF/lib/include -I$WORKSPACE/libMXF/examples/avidmxfinfo $DVS_CPPFLAGS"
CXXFLAGS="$CXXFLAGS $WX_CPPFLAGS"
#LDFLAGS="$LDFLAGS -L$ACE_ROOT/lib -lTAO -lACE -lTAO_CosNaming -lTAO_PortableServer -lTAO_AnyTypeCode -lTAO_Valuetype -lTAO_Messaging -lTAO_PI -lTAO_CodecFactory $IDLLOC/$IDLLIB $WORKSPACE/player/IngexPlayer/$PLAYERLIB $WORKSPACE/common/libcommon.a $WORKSPACE/common/YUVlib/libYUVlib.a -lX11 -lXv -lpthread -lfreetype -lfontconfig -luuid -ld3mxfinfo $WORKSPACE/libMXF/examples/reader/libMXFReader.a $WORKSPACE/libMXF/examples/avidmxfinfo/libavidmxfinfo.a $WORKSPACE/libMXF/lib/libMXF.a -lavformat -lavcodec -lavutil -lmp3lame -lx264 -lfaac -lfaad -lbz2 -lportaudio $DVS_LDFLAGS"
LIBS="-lTAO -lACE -lTAO_CosNaming -lTAO_PortableServer -lTAO_AnyTypeCode -lTAO_Valuetype -lTAO_Messaging -lTAO_PI -lTAO_CodecFactory -lX11 -lXv -lpthread -lfreetype -lfontconfig -luuid -lcommon -lYUVlib -ld3mxfinfo -lavidmxfinfo -lMXFReader -lMXF -lavformat -lavcodec -lavutil -lswscale -lmp3lame -lx264 -lfaac -lfaad -lbz2 -lportaudio"
LIBDIRS="-L$ACE_ROOT/lib -L$WORKSPACE/libMXF/lib -L$WORKSPACE/libMXF/examples/avidmxfinfo -L$WORKSPACE/libMXF/examples/archive/info -L$WORKSPACE/libMXF/examples/reader -L$WORKSPACE/common -L$WORKSPACE/common/YUVlib"
LDFLAGS="$LDFLAGS $LIBS $LIBDIRS $IDLLOC/$IDLLIB $WORKSPACE/player/IngexPlayer/$PLAYERLIB $DVS_LDFLAGS"

AC_SUBST(WX_LIBS)

AC_OUTPUT(Makefile src/Makefile)
